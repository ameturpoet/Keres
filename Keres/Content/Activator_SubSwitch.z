/********************************************************
*
*File:      Activator_SubSwitch
*Author:    Alex DeJardin
*Description:
    Contains functionality for an Activator that is used
    by the Activator_Multiswitch for sequence input
    
*Other Contributors:
    
********************************************************/
class Activator_SubSwitch : ZilchComponent
{
    //All properties are only used for combination locks
    [Property]
    var MaxValue : Integer = 2; //This bit's max value
    [Property]
    var MinValue : Integer = 0; //This bit's min value
    [Property]
    var CorrectValue : Integer = 2; //The correct value
    var CurrentValue : Integer = 1; //The current value
    [Property]
    var ShowText : Boolean = true; //Whether to show the current value
    var Combination : Boolean = false; //Whether or not this is used in a combination lock
    var Text : Cog = null; //The text cog
    
    function Initialize(init : CogInitializer)
    {
        this.CurrentValue = this.MinValue;
        
        //If this is part of a combination lock
        if (this.Owner.Parent.Activator_Multiswitch.LockType == SwitchType.Combination)
        {
            //Sets all the necessary properties
            this.Combination = true;
            this.Text = this.Owner.FindChildByName("text");
            var sb = new StringBuilder();
            sb.Write(this.CurrentValue);
            
            if (this.ShowText)
            {
                this.Text.SpriteText.Text = sb.ToString();
            }
            else
            {
                this.Text.SpriteText.Visible = false;
            }
        }
    }
    
    //Sends this object's name to the parent object
    function Activate()
    {
        //If the switch belongs to a combination lock
        if (this.Combination)
        {
            //Moves the current value along by 1
            this.CurrentValue += 1;
            
            //If the current value is greater than the max value, reset it to the min value
            if (this.CurrentValue > this.MaxValue)
            {
                this.CurrentValue = this.MinValue;
            }
            
            var sb = new StringBuilder();
            sb.Write(this.CurrentValue);
            
            this.Text.SpriteText.Text = sb.ToString();
        }
        
        //Updates the sequence of the parent object
        this.Owner.Parent.Activator_Multiswitch.UpdateSequence(this.Owner.Name);
    }
}
