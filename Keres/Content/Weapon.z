class Weapon : ZilchComponent
{
    [Property]
    var Projectile : Archetype = null;
    [Property]
    var RibbonObject : Archetype = null;
    [Property]
    var ProjectileNode : CogPath = null;
    [Property]
    var EnergyUse : Integer = 5;
    
    var MouseState : Boolean = false;
    var OldMState : Boolean = false;
    var StartLocation : Real3 = Real3(0, 0, 0);
    
    function Initialize(init : CogInitializer)
    {
        this.StartLocation = this.Owner.Transform.Translation;
    }

    function UpdateGun(deltaTime : Real, impactLocation : Real3, playerStats : PlayerStats)
    {
        this.MouseState = Zero.Mouse.IsButtonDown(MouseButtons.Left);
        if (playerStats.Energy >= this.EnergyUse && (this.MouseState != this.OldMState && this.MouseState))
        {
            this.Fire(impactLocation);
            playerStats.Energy -= this.EnergyUse;
        }
        this.OldMState = this.MouseState;
    }
    
    function Fire(impactLocation : Real3)
    {
        this.Owner.Space.CreateAtPosition(this.Projectile, impactLocation);
        var ribbon = this.Owner.Space.CreateAtPosition(this.RibbonObject, impactLocation);
        ribbon.RibbonParticle.Start = this.ProjectileNode.Cog.Transform.WorldTranslation;
        ribbon.RibbonParticle.End = impactLocation;
        ribbon.RibbonParticle.CalculateScale();
    }
}
