/********************************************************
*
*File:      EnemyAI
*Author:    Alex DeJardin
*Description:
    Causes enemies to move around and attack the player
    
*Other Contributors:
    
********************************************************/

enum EnemyState { Idle, Aware, Aggroed, Stunned, Distracted }

class EnemyAI : ZilchComponent
{
    [Property]
    var AttackDamage : Integer = 50; //The enemy's attack damage
    var CurrentTile : Cog = null; //The current pathing tile attached to
    var Disabled : Boolean = true; //Whether the enemy is disabled or not
    var TargetTile : Cog = null; //The next pathing tile to move to
    var ToTargetVector : Real3 = Real3(0, 0, 0); //The vector going to the next tile
    var ToPlayerRay : Ray = Ray(); //The ray to cast at the player for line-of-sight
    var RayFilter : CastFilter = CastFilter(); //The ray filter to cast with
    var State : EnemyState = EnemyState.Idle; //The enemy's current state
    var Player : Cog = null; //Pointer to the player
    var PlayerSightCooldown : Real = 0.0; //Cooldown from the last time the player was seen, enemy gives up pursuit after a while
    var SeesPlayer : Boolean = false; //Whether or not it sees the player right now
    var LevelGeometry : Cog = null; //Pointer to the level geometry (and the pathing grid)
    var DistractingLocation : Real3 = Real3(0, 0, 0); //The location the enemy is being distracted from
    var StunTime : Real = 0.0; //The counter for when the enemy is stunned
    [Property]
    var MaxStunTime : Real = 3; //The amount of time the enemy will be stunned for
    [Property]
    var Speed : Real = 1.0; //Movement speed
    [Property]
    var SightDistance : Real = 5.0; //Minimum sight distance for becoming aggroed
    [Property]
    var MaxSightDistance : Real = 20.0; //The maximum sight distance for detecting the player
    [Property]
    var AttentionSpanTime : Real = 3.0; //Max amount of time it takes to lose interest and become idle
    [Property]
    var AttackCooldown : Real = 0.3; //The cooldown between attacks for the enemy
    [Dependency]
    var Soundbox : Soundbox_Keres = null; //The enemy's soundbox
    var FootstepCounter : Real = 0.0; //Counter for the footstep sound
    var BangTimeCooldown : Real = 0.0; //Counter for the door-banging sound effect. Yes, the name is absurd
    var CurrentCooldown : Real = 0.0; //Counter for attacking the player
    var ZappedEffect : Cog = null; //The current particle effect for when the enemy is paralyzed
    
    function Initialize(init : CogInitializer)
    {
        //Connects all the things
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Space, "ReadyToPlay", this.Enable);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.StartCollide);
        Zero.Connect(this.Owner, Events.CollisionPersisted, this.AttackPlayer);
        //Sets pointers and initializes the player sight ray
        this.Player = this.Owner.Space.FindObjectByName("Player");
        this.LevelGeometry = this.Owner.Space.FindObjectByName("LevelGeometry");
        this.RayFilter.CollisionGroup = CollisionGroup.Find("LineofSightRayGroup");
        this.Owner.Transform.Translation *= Real3(1.0, 1.0, 0.0);
        //Sets the name to enemy for compatibility reasons (most are named enemy anyway, but still)
        this.Owner.Name = "enemy";
    }
    
    //Enables the enemy once the map has been generated
    function Enable(event : ReadyEvent)
    {
        this.Disabled = false;
    }
    
    //Attempts to distract the enemy, won't work if they have direct line-of-sight to the player
    function Distract(locationOfDistraction : Real3)
    {
        if (!this.SeesPlayer)
        {
            this.State = EnemyState.Distracted;
            this.DistractingLocation = locationOfDistraction;
        }
    }
    
    function StartCollide(event : CollisionEvent)
    {
        if (!this.Disabled)
        {
            //If the other object is the player and we're not on our cooldown
            if (event.OtherObject.Name == "Player" && this.CurrentCooldown <= 0.0 && this.State != EnemyState.Stunned)
            {
                //Attack the player
                event.OtherObject.PlayerStats.Life -= this.AttackDamage;
                event.OtherObject.PlayerControl_Move.Knockback(this.Owner.Transform.Translation);
                this.CurrentCooldown = this.AttackCooldown;
                event.OtherObject.Soundbox_Player.PlayHit();
                event.OtherObject.PlayerControl_Move.ShowHit();
                Console.WriteLine("Attacked");
            }
            //If the other object is a door
            else if (event.OtherObject.GatePiece != null && this.BangTimeCooldown <= 0.0)
            {
                //Play the 'banging on the door' sound and go idle
                this.Owner.Soundbox_Keres.PlayDoorBang();
                this.BangTimeCooldown = 0.6;
                Console.WriteLine("Doorbang");
                this.State = EnemyState.Idle;
            }
        }
    }
    
    //Attacks the player upon continued collision if they're really that dumb or they got stuck in a corner I guess
    function AttackPlayer(event : CollisionEvent)
    {
        if (event.OtherObject.Name == "Player" && this.CurrentCooldown <= 0.0 && this.State != EnemyState.Stunned)
        {
            if (!event.OtherObject.PlayerStats.DebugMode)
            {
                event.OtherObject.PlayerStats.Life -= this.AttackDamage;
            }
            this.CurrentCooldown = this.AttackCooldown;
            event.OtherObject.Soundbox_Player.PlayHit();
            event.OtherObject.PlayerControl_Move.ShowHit();
            Console.WriteLine("Attacked");
        }
    }

    //Updates the enemy
    function OnLogicUpdate(event : UpdateEvent)
    {
        //Decreases the attack cooldown if the enemy recently attacked
        if (this.CurrentCooldown > 0.0)
        {
            this.CurrentCooldown -= event.Dt;
        }
        
        if (!this.Disabled && this.CurrentCooldown <= 0.0)
        {
            //Decrements the door-bang cooldown
            if (this.BangTimeCooldown > 0.0)
            {
                this.BangTimeCooldown -= event.Dt;
            }
            
            //If the enemy's state is Aware or Distracted, it moves along the grid and checks for line-of-sight to the player
            if (this.State == EnemyState.Aware || this.State == EnemyState.Distracted)
            {
                this.MoveByGrid(event.Dt);
                this.CheckForPlayer(event.Dt);
                if (this.FootstepCounter <= 0.0)
                {
                    this.FootstepCounter = 1.4;
                    this.Soundbox.PlayFootstep();
                    this.GetCurrentTile();
                    this.GetNextTile();
                }
            }
            //If the enemy is aggroed, it moves directly towards the player and checks for line-of-sight to the player
            //If line-of-sight is lost, then the player is behind a wall or something, so it reverts back to the grid-based movement
            else if (this.State == EnemyState.Aggroed)
            {
                //Console.WriteLine("Aggoed");
                this.MoveTowardsPlayer(event.Dt);
                this.CheckForPlayer(event.Dt);
                if (this.FootstepCounter <= 0.0)
                {
                    this.FootstepCounter = 1.4;
                    this.Soundbox.PlayFootstep();
                }
            }
            //If the enemy is stunned, it doesn't do anything until the stun cooldown is over, at which point it returns to Aware
            else if (this.State == EnemyState.Stunned)
            {
                //Console.WriteLine("Stunned");
                this.BeStunned(event.Dt);
            }
            //Otherwise, the enemy is idle and doesn't do anything
            else
            {
                //Console.WriteLine("Idle");
                this.CheckForPlayer(event.Dt);
                if (this.CurrentTile != null)
                {
                    this.GetNextTile();
                }
                else
                {
                    this.GetCurrentTile();
                }
            }
        }
    }
    
    //Stuns the enemy, holding it in place for a period of time
    function Stun(stunEffect : Cog)
    {
        if (this.ZappedEffect != null)
        {
            stunEffect.Destroy();
        }
        else
        {
            this.ZappedEffect = stunEffect;
        }
        this.State = EnemyState.Stunned;
        this.StunTime = this.MaxStunTime;
        this.Owner.SoundEmitter.Stop();
        Console.WriteLine("Stunned");
        this.Owner.RigidBody.Static = true;
        this.Owner.RigidBody.Velocity *= 0;
    }
    
    //Does nothing for as long as the enemy is stunned
    function BeStunned(deltaTime : Real)
    {
        //If the enemy is no longer stunned
        if (this.StunTime <= 0.0)
        {
            //Makes the enemy aware and deletes the stunned particle effect
            this.State = EnemyState.Aware;
            if (this.ZappedEffect != null)
            {
                this.ZappedEffect.Destroy();
                this.ZappedEffect = null;
            }
            this.Owner.RigidBody.Static = false;
            this.GetCurrentTile();
            this.GetNextTile();
        }
        this.StunTime -= deltaTime;
    }
    
    //Moves directly towards the player
    function MoveTowardsPlayer(deltaTime : Real)
    {
        this.Owner.RigidBody.Velocity = this.Speed * Math.Normalize(this.Player.Transform.Translation - this.Owner.Transform.Translation);
        this.FootstepCounter -= deltaTime;
    }
    
    //Moves along a grid towards the player
    function MoveByGrid(deltaTime : Real)
    {
        //If the enemy has a tile to move to
        if (this.TargetTile != null)
        {
            //Gets the distance from the enemy to the tile
            var temp = Math.Distance(this.Owner.Transform.Translation, this.TargetTile.Transform.WorldTranslation * Real3(1, 1, 0));
            
            //Sets the velocity to 0, necessary for certain situations that would otherwise make the
            //enemy run straight into a wall
            this.Owner.RigidBody.Velocity *= 0;
            
            //If the length is greater than the distance the enemy will move during this frame, set the enemy's velocity
            if (temp > 0.04)
            {
                this.Owner.RigidBody.Velocity = this.Speed * this.ToTargetVector;
            }
            //If the enemy is close enough to the next tile, set the current tile to the next tile and get the next tile from there
            else
            {
                this.CurrentTile = this.TargetTile;
                this.GetNextTile();
            }
        }
        //If the enemy doesn't have a tile to move to
        else
        {
            //Find the next tile to move to, oddly enough
            this.GetNextTile();
        }
        
        this.FootstepCounter -= deltaTime;
    }
    
    //Uses rayscanning to check for direct line-of-sight to the player, setting the enemy state as a result
    function CheckForPlayer(deltaTime : Real)
    {
        if (this.Owner.DebugEnemyState != null)
        {
            
        }
        else
        {
        //Initialize the ray to cast
        this.ToPlayerRay.Start = this.Owner.Transform.Translation;
        this.ToPlayerRay.Direction = Math.Normalize(this.Player.Transform.Translation - this.Owner.Transform.Translation);
        
        //Cast the ray
        var detectPlayer = this.Space.PhysicsSpace.CastRayFirstFiltered(this.ToPlayerRay, this.RayFilter);
        
        //If the player was hit by the ray
        if (detectPlayer.ObjectHit != null && detectPlayer.ObjectHit.Name == "Player" && detectPlayer.Distance <= this.MaxSightDistance)
        {
            this.SeesPlayer = true;
            
            if (this.State == EnemyState.Idle)
            {
                this.Owner.Soundbox_Keres.PlayAware();
                var musicBox = this.Owner.Space.FindObjectByName("MusicBox");
                
                if (musicBox != null)
                {
                    musicBox.MusicBox.KeresFollowing += 1;
                    musicBox.MusicBox.PlayMusic();
                }
            }
            
            //If the player is within sight distance, immediately begin aggroing and head straight towards him
            if (Math.Distance(this.Player.Transform.Translation, this.Owner.Transform.Translation) <= this.SightDistance)
            {
                if (this.State != EnemyState.Aggroed)
                {
                    this.State = EnemyState.Aggroed;
                    Console.WriteLine("Aggroed");
                    this.PlayerSightCooldown = this.AttentionSpanTime;
                }
            }
            //Otherwise, become aware of the player's presence and navigate towards his location
            else
            {
                if (this.State != EnemyState.Aware)
                {
                    this.State = EnemyState.Aware;
                    Console.WriteLine("Aware");
                    this.PlayerSightCooldown = this.AttentionSpanTime;
                    this.GetCurrentTile();
                    this.GetNextTile();
                }
            }
        }
        //If no line-of-sight is acquired, but the enemy is aggroed, then the player is behind something that the enemy
        //has to navigate around, so it becomes Aware and uses grid navigation instead of making a beeline
        else if (this.State == EnemyState.Aggroed)
        {
            if (this.State != EnemyState.Aware)
            {
                this.SeesPlayer = false;
                this.State = EnemyState.Aware;
                Console.WriteLine("Aware");
                this.PlayerSightCooldown = this.AttentionSpanTime;
                this.GetCurrentTile();
                this.GetNextTile();
            }
        }
        //If the player isn't hit by the ray, meaning that the enemy doesn't have direct line-of-sight
        else
        {
            if (this.State != EnemyState.Idle && this.State != EnemyState.Distracted)
            {
                this.SeesPlayer = false;
                //Counts down the attention span timer
                this.PlayerSightCooldown -= deltaTime;
                //If the player has been out of sight for the length of the enemy's attention span, the enemy resumes being idle
                if (this.PlayerSightCooldown <= 0.0)
                {
                    Console.WriteLine("Idle");
                    this.State = EnemyState.Idle;
                    this.Owner.RigidBody.Velocity *= 0;
                    var musicBox = this.Owner.Space.FindObjectByName("MusicBox");
                
                    if (musicBox != null)
                    {
                        musicBox.MusicBox.KeresFollowing -= 1;
                        musicBox.MusicBox.PlayMusic();
                    }
                }
            }
        }
        }
    }
    
    //Gets the next tile to go to in the pathing grid
    function GetNextTile()
    {
        //Find the first tile of the lowest weight
        foreach (var ti in this.CurrentTile.NavigationPoint.NearPoints)
        {
            if (ti.NavigationPoint.Weight < this.CurrentTile.NavigationPoint.Weight)
            {
                this.TargetTile = ti;
            }
        }
        
        //If a tile was found (always gonna happen), set the movement vector towards it
        if (this.TargetTile != null)
        {
            this.ToTargetVector = Math.Normalize((this.TargetTile.Transform.WorldTranslation - this.Owner.Transform.Translation) * Real3(1, 1, 0));
        }
        
    }
    
    //Finds the closest tile to the enemy
    function GetCurrentTile()
    {
        var tiles = this.Space.FindAllObjectsByName("Floor");
        var currentDistance = -1337.0;
        
        //Scans through each tile and finds the closest one
        foreach (var ti in tiles)
        {
            var tempLocation = ti.Transform.WorldTranslation - this.Owner.Transform.WorldTranslation;
            var distance = Math.Pow(tempLocation.X, 2) + Math.Pow(tempLocation.Y, 2);
            
            if (!ti.NavigationPoint.FortyFiveCorner && (currentDistance < 0 || distance < currentDistance))
            {
                this.CurrentTile = ti;
                currentDistance = distance;
            }
        }
    }
}
