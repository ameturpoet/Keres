class Interactable_Gate : ZilchComponent
{
    [Dependency]
    var Interactable : Interactable = null;
    [Property]
    var PlayerCantOpen : Boolean = false;
    [Property]
    var Locked : Boolean = false;
    [Property]
    var Key : String = "";
    [Property]
    var OpenTime : Real = 1.0;
    
    var CurrentOpenTime : Real = 0.0;
    
    var State : Integer = 2;
    //4 states:
    //0-Open
    //1-Opening
    //2-Closed
    //3-Closing
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.Update);
    }
    
    function Toggle()
    {
        if (this.State == 0 || this.State == 1)
        {
            this.State = 3;
        }
        else if (this.State == 2 || this.State == 3)
        {
            this.State = 1;
        }
    }
    
    function UpdateSubObjects()
    {
        var pieces = this.Owner.FindAllChildrenByName("doorPiece");
        
        foreach (var pi in pieces)
        {
            pi.GatePiece.SetOpenPosition(this.CurrentOpenTime / this.OpenTime);
        }
    }

    function Update(event : UpdateEvent)
    {
        if (this.State == 1)
        {
            this.CurrentOpenTime += event.Dt;
            if (this.CurrentOpenTime >= this.OpenTime)
            {
                this.State = 0;
                this.CurrentOpenTime = this.OpenTime;
            }
            this.UpdateSubObjects();
        }
        else if (this.State == 3)
        {
            this.CurrentOpenTime -= event.Dt;
            if (this.CurrentOpenTime <= 0.0)
            {
                this.State = 2;
                this.CurrentOpenTime = 0.0;
            }
            this.UpdateSubObjects();
        }
    }
}
